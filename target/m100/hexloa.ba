10 CLEAR 128,49993
20 DEFSTR A:DEFINT B,I,L,S

30 OPEN "COM:78N1E" FOR INPUT AS 1
40 IF EOF(1) THEN 150
50 LINE INPUT #1,A$
60 PRINT ".";

69 REM end of file
70 B%=ASC(MID$(A$,9,1)):IF B%=49 THEN 150

79 REM data
80 IF B%<>48 THEN 40
90 I%=2:GOSUB 200:S%=B%
100 I%=4:GOSUB 200:P=256*B%:I%=6:GOSUB 200:P=P+B%
110 FOR I%=10 TO 8+S%+S% STEP 2
120 GOSUB 200:POKE P,B%:P=P+1
130 NEXT I%
140 GOTO 40

150 CLOSE 1
160 CALL 50000
170 END

199 REM read hex byte
200 B%=ASC(MID$(A$,I%,1))-48:IF B%>9 THEN B%=B%-7
210 L%=ASC(MID$(A$,I%+1,1))-48:IF L%>9 THEN L%=L%-7
220 B%=(B%*16)+L%
230 RETURN
